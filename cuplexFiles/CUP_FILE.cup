/*************/
/* IMPORT(S) */
/*************/
import java_cup.runtime.*;
import AST.*;
import java.util.ArrayList;


parser code 
{:
	public Lexer lexer;

	public Parser(Lexer lexer)
	{
		super(lexer);
		this.lexer = lexer;
	}
:}

/************************/
/* CUSTOMIZE CUP PARSER */
/************************/
scan with
{:
	Symbol s;
	s = lexer.next_token();
	return s; 
:};



/*************/
/* TERMINALS */
/*************/
terminal IF;
terminal DOT;
terminal PLUS;
terminal MINUS;
terminal TIMES;
terminal DIVIDE;
terminal LPAREN;
terminal RPAREN;
terminal LBRACK;
terminal RBRACK;
terminal LBRACE;
terminal RBRACE;
terminal SEMICOLON;
terminal ASSIGN;

terminal CLASS;
terminal COMMA;
terminal EQUAL;
terminal EXTENDS;
terminal GT;
terminal GTE;
terminal INTEGER;
terminal NEW;
terminal LT;
terminal LTE;
terminal NEQUAL;
terminal NULL;
terminal RETURN;
terminal STRING;
terminal VOID;
terminal WHILE;

/*************/
/* TERMINALS */
/*************/
terminal Integer INT;
terminal String ID;
terminal String STR;
terminal String	CLASS_ID;

non terminal AST_Exp exp;
non terminal AST_Variable var;
non terminal AST_Stmt stmt;
non terminal ArrayList<AST_Stmt> stmtList;
non terminal AST_Program program;
non terminal ArrayList<AST_ClassDecl> classDeclLst;
non terminal AST_ClassDecl classDecl;
non terminal ArrayList<AST_FieldOrMethod> fieldMethodList;
non terminal AST_Type type;
non terminal AST_VirtualCall virtualCall;
non terminal ArrayList<AST_Exp> expList;
non terminal AST_Literal literal;
non terminal AST_Binop binop;
non terminal ArrayList<AST_FuncArgument> formals;
non terminal ArrayList<AST_FuncArgument> commaTypeIDList;
non terminal AST_FuncArgument commaTypeID;
non terminal AST_FieldList field;
non terminal ArrayList<String> idList;
non terminal AST_Method method;

/***********************/
/* OPERATOR PRECEDENCE */
/***********************/
precedence left PLUS;
precedence left MINUS;
precedence left TIMES;
precedence left DIVIDE;
precedence left LT;
precedence left LTE;
precedence left GT;
precedence left GTE;
precedence left EQUAL;
precedence left NEQUAL;
precedence right ASSIGN;
precedence left LBRACK, RBRACK, LPAREN, RPAREN, DOT;
precedence left IF;

/************************/
/* START WITH: program */
/************************/
start with program;

/********************/
/* DERIVATION RULES */
/********************/

stmtList	::=		stmt:s							{: RESULT = new ArrayList<AST_Stmt>(); RESULT.add(s); :}
					| stmtList:l stmt:s			    {: l.add(s); RESULT = l; :}
					;

var			::=		ID:name								{: RESULT = new AST_VariableSimpleField(name);       :}
					| exp:e DOT ID:fieldName			{: RESULT = new AST_VariableExpField(e,fieldName); :} 
					| exp:e1 LBRACK exp:e2 RBRACK		{: RESULT = new AST_VariableNestedExp(e1,e2);     :} 	
					;
					
program		::=		classDeclLst:cdl					{: RESULT = new AST_ProgramClassDeclList(cdl);  :}
					;


classDeclLst::=		classDecl:cd						{: RESULT = new ArrayList<AST_ClassDecl>(); RESULT.add(cd); :}
					| classDeclLst:l classDecl:cd		{: l.add(cd); RESULT = l; :}
					;
					
classDecl	::=		CLASS CLASS_ID:name LBRACE fieldMethodList:l RBRACE  {:  RESULT = new AST_ClassDecl(name,null,l); :}
					| CLASS CLASS_ID:name LBRACE RBRACE {: RESULT = new AST_ClassDecl(name,null,null); :}
					| CLASS CLASS_ID:name EXTENDS CLASS_ID:otherName LBRACE fieldMethodList:l RBRACE {: RESULT = new AST_ClassDecl(name,otherName,l); :}
					| CLASS CLASS_ID:name EXTENDS CLASS_ID:otherName LBRACE RBRACE {: RESULT = new AST_ClassDecl(name,otherName,null); :}
					;				

fieldMethodList ::=  field:f 	{: RESULT = new ArrayList<AST_FieldOrMethod>(); RESULT.add(f); :}
					| method:m 	{: RESULT = new ArrayList<AST_FieldOrMethod>(); RESULT.add(m); :}
					| fieldMethodList:l field:f {: l.add(f); RESULT = l; :}  
					| fieldMethodList:l method:m {: l.add(m); RESULT = l; :}
					;

stmt		::=		var:v ASSIGN exp:e SEMICOLON								{: RESULT = new AST_StmtVarAssignment(v,e); :}
					| IF    LPAREN exp:cond RPAREN  stmt:s 						{: RESULT = new AST_StmtIf(cond,s); :}
					| WHILE LPAREN exp:cond RPAREN  stmt:s 						{: RESULT = new AST_StmtWhile(cond,s); :}
					| virtualCall:cl SEMICOLON											{: RESULT = new AST_StmtCall(cl); :}
					| RETURN SEMICOLON											{: RESULT = new AST_StmtReturn(null); :}
					| RETURN exp:e SEMICOLON									{: RESULT = new AST_StmtReturn(e); :}
					| LBRACE stmtList:body RBRACE								{: RESULT = new AST_StmtMultList(body); :}
					| LBRACE RBRACE												{: RESULT = new AST_StmtMultList(null); :}
					| type:t ID:name SEMICOLON									{: RESULT = new AST_StmtVariableExp(t,name,null); :}
					| type:t ID:name ASSIGN exp:e SEMICOLON						{: RESULT = new AST_StmtVariableExp(t,name,e); :}
					;
					
type		::= 	INTEGER  {: RESULT = new AST_TypePrimitive(PrimitiveDataTypes.INT); :}
					| STRING  {: RESULT = new AST_TypePrimitive(PrimitiveDataTypes.STRING); :}
					| CLASS_ID:name {: RESULT = new AST_TypeClass(name); :}
					| type:t LBRACK RBRACK {: RESULT = new AST_TypeArray(t); :}
					;

	
virtualCall ::= 	exp:e DOT ID:name LPAREN expList:elst RPAREN      {: RESULT = new AST_VirtualCall(e,name,elst); :}
					| ID:name LPAREN expList:elst RPAREN			  {: RESULT = new AST_VirtualCall(null,name,elst); :}
					| ID:name LPAREN RPAREN			                  {: RESULT = new AST_VirtualCall(null,name,null); :}
					| exp:e DOT ID:name LPAREN RPAREN			      {: RESULT = new AST_VirtualCall(e,name,null); :}
					;
					
				
exp          ::=       var:v                      					  {: RESULT = new AST_ExpVariable(v);          :}
                       | virtualCall:cl                    				  {: RESULT = new AST_ExpFunctionCall(cl);          :}
                       | exp:e1 binop:op exp:e2            			  {: RESULT = new AST_BinopExp(e1, e2, op); :}
                       | NEW type:t LBRACK exp:e RBRACK    			  {: RESULT = new AST_ExpNewTypeArray(t, e); :}
                       | NEW CLASS_ID:name LPAREN RPAREN   			  {: RESULT = new AST_ExpNewClass(name); :}
                       | literal:l   							      {: RESULT = new AST_ExpLiteral(l); :}
                       | LPAREN exp:e RPAREN    					  {: RESULT = new AST_ExpInsideParens(e); :}
                       ;


expList ::= 	exp:e 	{: RESULT = new ArrayList<AST_Exp>(); RESULT.add(e); :}
				|  expList:l COMMA exp:e  {: l.add(e); RESULT = l;  :}
				;
 
literal     ::=     INT:num                              {: RESULT = new AST_LiteralNumber(num); :}
                    | STR:str                            {: RESULT = new AST_LiteralString(str); :}
                    | NULL                               {: RESULT = new AST_LiteralNull(); :}
                    ;

binop       ::=     MINUS                                {: RESULT = new AST_Binop(BinaryOpTypes.MINUS); :}
					| PLUS								 {: RESULT = new AST_Binop(BinaryOpTypes.PLUS); :}
                    | TIMES                                {: RESULT = new AST_Binop(BinaryOpTypes.TIMES); :}
                    | DIVIDE                                {: RESULT = new AST_Binop(BinaryOpTypes.DIVIDE);:}
                    | LT                                {: RESULT = new AST_Binop(BinaryOpTypes.LT); :}
                    | LTE                                {: RESULT = new AST_Binop(BinaryOpTypes.LTE); :}
                    | GT                                {: RESULT = new AST_Binop(BinaryOpTypes.GT); :}
                    | GTE                                {: RESULT = new AST_Binop(BinaryOpTypes.GTE); :}
                    | EQUAL                                {: RESULT = new AST_Binop(BinaryOpTypes.EQUALS); :}
                    | NEQUAL                                {: RESULT = new AST_Binop(BinaryOpTypes.NEQUALS); :}
                    ;

formals    ::=      type:t ID:name                      {: RESULT = new ArrayList<AST_FuncArgument>(); RESULT.add(new AST_FuncArgument(t,name));:}
                    | commaTypeIDList:l type:t ID:name   {: l.add(new AST_FuncArgument(t,name)); RESULT = l; :}
                    ;

commaTypeIDList ::= commaTypeID:cmi                     {: RESULT = new ArrayList<AST_FuncArgument>(); RESULT.add(cmi); :}
                    | commaTypeIDList:l commaTypeID:cmi {: l.add(cmi); RESULT = l; :}
                    ;

commaTypeID     ::= COMMA type:t ID:name                {: RESULT = new AST_FuncArgument(t, name);:}
                    ;

field           ::=	type:t idList:l SEMICOLON {: RESULT = new AST_FieldList(t,l); :}
					;
				
				
idList			::= ID:name					  {: RESULT = new ArrayList<String>(); RESULT.add(name); :}
					| idList:l COMMA ID:name  {: l.add(name); RESULT= l; :}
					;

method			::= type:t ID:name LPAREN RPAREN LBRACE RBRACE {: RESULT= new AST_Method(t,name,null,null); :}
					| type:t ID:name LPAREN formals:f RPAREN LBRACE RBRACE {: RESULT= new AST_Method(t,name,f,null); :}
					| type:t ID:name LPAREN formals:f RPAREN LBRACE stmtList:l RBRACE {: RESULT = new AST_Method(t,name,f,l); :}
					| type:t ID:name LPAREN RPAREN LBRACE stmtList:l RBRACE {: RESULT = new AST_Method(t,name,null,l); :}
					| VOID ID:name LPAREN RPAREN LBRACE RBRACE {: RESULT= new AST_Method(new AST_TypePrimitive(PrimitiveDataTypes.VOID),name,null,null); :}
					| VOID ID:name LPAREN formals:f RPAREN LBRACE RBRACE {: RESULT= new AST_Method(new AST_TypePrimitive(PrimitiveDataTypes.VOID),name,f,null); :}
					| VOID ID:name LPAREN formals:f RPAREN LBRACE stmtList:l RBRACE {: RESULT = new AST_Method(new AST_TypePrimitive(PrimitiveDataTypes.VOID),name,f,l); :}
					| VOID ID:name LPAREN RPAREN LBRACE stmtList:l RBRACE {: RESULT = new AST_Method(new AST_TypePrimitive(PrimitiveDataTypes.VOID),name,null,l); :}
				    ;

				    